buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}"
  }
}

plugins {
  id("io.spinnaker.plugin.bundler").version("$spinnakerGradleVersion")
  id("com.palantir.git-version").version("0.12.2")
  id("org.jetbrains.kotlin.jvm").version("$kotlinVersion")
  id("org.jetbrains.kotlin.kapt").version("$kotlinVersion")
}

repositories {
  mavenCentral()
}

spinnakerBundle {
  pluginId = "Pulumi.PreConfiguredJobPlugin"
  description = "A Pulumi plugin that provides Pulumi pipeline stage as a pre-configured job."
  provider = "https://github.com/$pluginOwner"
  version = rootProject.version
}

version = normalizedVersion()

subprojects {
  group = "com.pulumi.plugin"
  version = rootProject.version
}

String normalizedVersion() {
  String fullVersion = gitVersion()
  String normalized = fullVersion.split("-").first()
  if (fullVersion.contains("dirty")) {
    return "$normalized-SNAPSHOT"
  } else {
    return normalized
  }
}
